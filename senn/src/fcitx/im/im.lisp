(defpackage :senn.fcitx.im
  (:use :cl)
  (:export :inputting
           :make-inputting
           :inputting-buffer
           :inputting-predictions

           :katakana
           :make-katakana
           :katakana-input

           :selecting-from-predictions
           :make-selecting-from-predictions
           :selecting-from-predictions-predictions
           :selecting-from-predictions-current-index
           :selecting-from-predictions-current-input
           :selecting-from-predictions-move-prediction

           :converting
           :make-converting
           :converting-pronunciation
           :converting-segments
           :converting-current-segment
           :converting-current-input
           :converting-current-segment-index
           :converting-move-curret-segment

           :editing-view/inputing-state
           :editing-view/katakana-state
           :editing-view/selecting-from-predictions
           :converting-view/converting-state))
(in-package :senn.fcitx.im)

(defstruct inputting
  (buffer (senn.im.buffer:make-buffer))
  predictions)


(defstruct (katakana (:constructor %make-katakana))
  (input ""))

(defun make-katakana (&key input)
  (%make-katakana :input (senn.ja:hiragana->katakana input)))


(defstruct selecting-from-predictions predictions current-index)

(defun selecting-from-predictions-current-input (s)
  (nth (selecting-from-predictions-current-index s)
       (selecting-from-predictions-predictions s)))

(defun selecting-from-predictions-move-prediction (s diff)
  (let ((new-index (+ (selecting-from-predictions-current-index s) diff)))
    (when (<= 0 new-index
              (1- (length (selecting-from-predictions-predictions s))))
      (setf (selecting-from-predictions-current-index s) new-index)))
  s)


(defstruct converting
  segments
  pronunciation
  (current-segment-index 0))

(defun converting-move-curret-segment (c diff)
  (let ((new-index (+ (converting-current-segment-index c) diff)))
    (when (<= 0 new-index (1- (length (converting-segments c))))
      (setf (converting-current-segment-index c) new-index)))
  c)

(defun converting-current-segment (c)
  (elt (converting-segments c)
       (converting-current-segment-index c)))

(defun converting-current-input (c)
  (format nil "窿磲疸狎＇箦铑轫箦珥孱艉箦珥孱舡沲蝌孱舡骘蝽ㄣ镱鲥螋轭绛箦珥孱趔悌┅换珠鬻ㄤ彐躅戾铉翳豸娓篝蜷铉戾铉翳ㄢ徕屐后趄轭绛麸镢翦趔篝蜷铉哄钽镤轭乎翩俯┅ㄤ彐躅怩骀弪沲蝮矧痫蟓豸娓ㄢ蹑驽颟戾è篝蜷铉箦铑轫怩骀弪衡蹑驽颦篝蜷铉怩骀弪┅ㄣ躜箫颦痫箦铑轫怩骀弪衡蹑驽颦沲蝮矧痫怩骀弪┅戾铉翳豸娓篚怏羼篝蜷铉沲蝮矧痫螬┅ㄤ彐躅磲脲邃轸轭绛鲩鬻ㄣ躜箫颦痫轭瘐痱邃殂糸镱痱邃殂糸镱轭溴泔眄轸翦洵篝蜷铉戾è牦镱牦秣詈铄鳝牦á沲蝮矧痫螈沲蝮矧痫螬á轭瘐簪轭瘐舂á痱邃殂糸镱螈痱邃殂糸镱螬á痱邃殂糸镱轭溴矧痱邃殂糸镱轭溴暴á泔眄轸翦洵轭瘐簪泔眄轸翦洵篝蜷铉┅┅ㄦ矧磲铋⑴纳陨吻立牦秣詈麸牦镱牦镱┅┅ㄤ彐躅邃轸轭绛鲩鬻轭瘐糸铉篝狒脲泔眄轸翦洵篝蜷铉戾è怩骀弪ㄩ铕豸糸铉怩骀弪螬┅磲脲邃轸轭绛鲩鬻ㄢ蹑驽颦沲蝮矧痫蟓豸娓怩骀弪箦铑轫怩骀弪衡蹑驽颦篝蜷铉怩骀弪ㄩ铕豸糸铉痱邃殂糸镱螬铋矧泔眄轸翦洵篝蜷铉┅┅ㄤ彐躅邃轸轭绛鲩鬻脶翎脶钺篝狒螬戾è脶翎脶钺轭瘐脶翎脶钺轭瘐螬┅磲脲邃轸轭绛鲩鬻戾铉翳豸娓脶翎脶钺轭瘐舂脶翎脶钺轭瘐铋铋┅ㄤ彐躅邃轸轭绛鲩鬻箦戾泗轭绛骝镯痱邃殂糸镱螬戾è轭瘐箦戾泗轭绛骝镯痱邃殂糸镱蟓沲蝌孱舡轭瘐螬┅磲脲邃轸轭绛鲩鬻戾铉翳豸娓轭瘐舂轭瘐箦戾泗轭绛骝镯痱邃殂糸镱蟓痱邃殂糸镱螬箦戾泗轭绛骝镯痱邃殂糸镱蟓沲蝌孱舡轭溴螬┅ㄤ彐躅泔铞弪糸铉鲩鬻泔铞弪糸铉篝狒螬戾è牦镱牦秣詈铄鳝牦á骘蝽螈磲疸狎＇箦铑轫箦珥孱艉箦珥孱舡沲蝌孱舡骘蝽ㄣ镱鲥螋轭绛箦珥孱趔螬┅á沲蝮矧骘蝽轭溴ㄣ镱鲥螋轭绛沲蝌孱舡箦珥孱舡轭溴螬á沲蝮矧骘蝽戾è箦珥孱ㄣ镱鲥螋轭绛沲蝌孱舡箦珥孱螬┅ㄩ箦铑轫箦珥孱艉箦珥孱舡箬秣蟓脶翎脶钺箦珥孱舂牦秣詈铄鳝牦á汜钿殇狒弩铋飑á汜钿殇狒瀛轭溴暴牦秣詈铄鳝牦á汜钿殇狒弩ㄩ箦铑轫箦珥孱艉箦珥孱舡栳蟓盹蝈汜钿殇狒弩箦珥孱舂铋箦铑轫箦珥孱艉箦珥孱舡骘蝽箦珥孱舂┅á汜钿殇狒瀛轭溴箦铑轫箦珥孱艉箦珥孱舡沲蝌孱舡轭溴箦珥孱舂┅┅┅┅ㄦ矧磲铋⒚衔峙以晌立牦秣詈麸牦镱牦镱┅┅